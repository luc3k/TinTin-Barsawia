#CLASS {gornictwo} OPEN

#ACTION {^Przestajesz kuc.$}
{
	#variable wykopywanie 0;
	#line gag;
	#echo {<faa>%0<088>}
}
{5}

#ACTION {^Wsrod gruzu dostrzegasz kilka okruchow jakiegos mineralu.$}
{
	#line gag;
	#variable komenda {wez wszystko};
	#echo {<afa>%0<faa>[$komenda]<088>}
}
{5}

#ACTION {^Zaczynasz kuc %1 sciane.$}
{
	#variable wykopywanie 1;
	#line gag;
	#echo {<afa>%0<088>}
}
{5}

#ALIAS {/gornik}
{
	#if {$kilof == 0}
	{
		#variable kilof 1;
		#regexp {${postacie[$nazwa_postaci][glowny_pojemnik]}} {plecak} {#variable dopelniacz plecaka};
		otworz ${postacie[$nazwa_postaci][glowny_pojemnik]};
		wez kilof z $dopelniacz;
		zamknij ${postacie[$nazwa_postaci][glowny_pojemnik]};
		#unvariable dopelniacz;
		#cr
	};
	#elseif {$kilof == 1}
	{
		#variable kilof 0;
		#regexp {${postacie[$nazwa_postaci][glowny_pojemnik]}} {plecak} {#variable dopelniacz plecaka};
		otworz ${postacie[$nazwa_postaci][glowny_pojemnik]};
		wloz kilof do $dopelniacz;
		zamknij ${postacie[$nazwa_postaci][glowny_pojemnik]};
		#unvariable dopelniacz;
		#cr
	}
}
{5}

#ALIAS {/gornikh}
{
	#showme {Potrzebne: };
	#showme {stemple (klody), lampa, olej do lampy, krzesiwo do rozpalania lampy, kilof i owalny przejrzysty krysztal od Balifa do znajdowania urobku};
	#showme {3 stemple podtrzymaja kopalnie, nie mniej};
	#showme {stempluj stemplem};
	#showme {zbliz krysztal do polnocnej sciany};
	#showme {kuj polnocna sciane};
	#showme {kj polnocna};
	#showme {zkr polnocnej};
	#showme {Czarna brylka to wegiel.};
	#showme {Ciemnoczerwona to Zelazo};
	#showme {Jasnoczerwona to miedz};
	#showme {Jasnoniebieska, bardzo zadko spotykana to orichalk};
	#showme {srebrzysta to srebro}
}
{5}

#ALIAS {/kuj}
{
	#switch {"%1"}
	{
		#case {"n"}
		{
			kuj polnocna sciane
		};
		#case {"s"}
		{
			kuj poludniowa sciane
		};
		#case {"e"}
		{
			kuj wschodnia sciane
		};
		#case {"w"}
		{
			kuj zachodnia sciane
		};
		#case {"ne"}
		{
			kuj polnocno-wschodnia sciane
		};
		#case {"se"}
		{
			kuj poludniowo-wschodnia sciane
		};
		#case {"nw"}
		{
			kuj polnocno-zachodnia sciane
		};
		#case {"sw"}
		{
			kuj poludniowo-zachodnia sciane
		};
		#default
		{
			#echo {<faa>Zly parametr<088>}
		}
	}
}
{5}

#ALIAS {krzes}
{
	#regexp {${postacie[$nazwa_postaci][glowny_pojemnik]}} {plecak} {#variable dopelniacz plecaka};
	otworz ${postacie[$nazwa_postaci][glowny_pojemnik]};
	wez krzemien z $dopelniacz;
	wez sztylet z pochwy;
	krzesaj krzemieniem o sztylet na lampe;
	wloz krzemien do $dopelniacz;
	wloz sztylet do pochwy;
	zamknij ${postacie[$nazwa_postaci][glowny_pojemnik]};
	#unvariable dopelniacz
}
{5}

#ALIAS {zkr}
{
	#regexp {${postacie[$nazwa_postaci][glowny_pojemnik]}} {plecak} {#variable dopelniacz plecaka};
	otworz ${postacie[$nazwa_postaci][glowny_pojemnik]};
	wez krysztal z $dopelniacz;
	#switch {"%1"}
	{
		#case {"n"}
		{
			zbliz krysztal do polnocnej sciany
		};
		#case {"s"}
		{
			zbliz krysztal do poludniowej sciany
		};
		#case {"e"}
		{
			zbliz krysztal do wschodniej sciany
		};
		#case {"w"}
		{
			zbliz krysztal do zachodniej sciany
		};
		#case {"ne"}
		{
			zbliz krysztal do polnocno-wschodniej sciany
		};
		#case {"se"}
		{
			zbliz krysztal do poludniowo-wschodniej sciany
		};
		#case {"nw"}
		{
			zbliz krysztal do polnocno-zachodniej sciany
		};
		#case {"sw"}
		{
			zbliz krysztal do poludniowo-zachodniej sciany
		};
		#default
		{
			#echo {<faa>Zly parametr<088>}
		}
	};
	wloz krysztal do $dopelniacz;
	zamknij ${postacie[$nazwa_postaci][glowny_pojemnik]};
	#unvariable dopelniacz
}
{5}

#SUBSTITUTE       {^Blade iskry poruszaja sie leniwie wewnatrz krysztalu.$}                                     {<faa>%0[BRAK KRUSZCA]<088>}   {5}
#SUBSTITUTE       {^Gdy zblizasz krysztal do sciany migoczace iskry wewnatrz stapiaja sie w wirujacy krag blasku, dajac znak o bliskosci mineralow.$}                                                                                                               {<afa>%0<088>}  {5}
#SUBSTITUTE       {^Gdy zblizasz krysztal do sciany migoczace iskry wewnatrz wyraznie przyspieszaja, dajac znak o bliskosci mineralow.$}                                                                                                  {<afa>%0<088>}  {5}
#SUBSTITUTE       {^Strop zabezpieczony jest stemplami.$}                   {<afa>%0<088>}  {5}
#VARIABLE         {kilof}  {0}
#VARIABLE         {wykopywanie}  {0}

#CLASS {gornictwo} CLOSE
