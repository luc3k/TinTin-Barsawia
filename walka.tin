#CLASS {walka} OPEN

#ACTION {^%1 uciekl{a|} ci.$}
{
	#variable enemy_run 1;
	#line gag;
	#echo {<efa>%1 <faa>uciekl%2 ci.<099>}
}
{5}

#ACTION {^%1 {w panice wybiega na|kica w panice na} %3.$}
{
	#line gag;
	#nop possible bug in if conditions so this looks like this for now;
	#if {&{postacie[$nazwa_postaci][ustawienia][pogon]}}
	{
		#if {${postacie[$nazwa_postaci][ustawienia][pogon]} == 1}
		{
			#echo {<ffa>%1 <faa>%2 <efa>%3.<faa>[%3]<099>};
			#if {$enemy_run == 1}
			{
				#variable run_direction {%3}
			}
		};
		#else
		{
			#echo {<ffa>%1 <faa>%2 <efa>%3.<099>}
		}
	};
	#else
	{
		#echo {<ffa>%1 <faa>%2 <efa>%3.<099>}
	}
}
{5}

#ACTION {^Atakujac %1 wychodzisz z ukrycia.$}
{
	#line gag;
	#echo {<fab>%0<099>};
	#variable ukrywanie 0
}
{5}

#ACTION {^Chowasz sie najlepiej jak potrafisz.$}
{
	#line gag;
	#echo {<afa>%0<099>};
	#variable ukrywanie 1
}
{5}

#ACTION {^Jest tu zbyt trudno sie schowac, wiec jestes widoczny z powrotem.$}
{
	#line gag;
	#echo {<fab>%0<099>};
	#variable ukrywanie 0
}
{5}

#ACTION {^Jestes tak zmeczony, ze nie mozesz przemykac sie w tym kierunku.$}
{
	#line gag;
	#echo {<fab>%0<099>};
	#map undo
}
{5}

#ACTION {^Wycinasz skore z ciala %1.$}
{
	#line gag;
	#variable wycinka_skor 1;
	#variable wycinka_komenda /schowajs;
	#echo {<afa>%0<faa>[/schowajs]<099>}
}
{5}

#ACTION {^Zabiles %* %S.$}
{
	#line gag;
	#echo {<afa>Zabiles <cfa>%1 <ffa>%2.<099>};
	#math {postacie[$nazwa_postaci][liczba_zabitych]} {${postacie[$nazwa_postaci][liczba_zabitych]}+1};
	#math {dzis_zabitych} {${dzis_zabitych}+1};
	#math {postacie[$nazwa_postaci][zabici][%2][ilosc]} {${postacie[$nazwa_postaci][zabici][%2][ilosc]}+1};
	#variable enemy_run 0;
	save
}
{5}

#ACTION {^{Wychodzisz z ukrycia.|Nie jestes juz schowany.}$}
{
	#line gag;
	#echo {<fab>%0<099>};
	#variable ukrywanie 0
}
{5}

#ALIAS {/d}
{
	dzgnij %1 sztyletem
}
{5}

#ALIAS {/schowajs}
{
	#regexp {${postacie[$nazwa_postaci][glowny_pojemnik]}} {plecak} {#variable dopelniacz plecaka};
	otworz ${postacie[$nazwa_postaci][glowny_pojemnik]};
	wloz skore do $dopelniacz;
	zamknij ${postacie[$nazwa_postaci][glowny_pojemnik]};
	#unvariable dopelniacz
}
{5}

#ALIAS {/sps}
{
	#regexp {${postacie[$nazwa_postaci][glowny_pojemnik]}} {plecak} {#variable dopelniacz plecaka};
	otworz ${postacie[$nazwa_postaci][glowny_pojemnik]};
	wez skory z $dopelniacz;
	sprzedaj skory;
	zamknij ${postacie[$nazwa_postaci][glowny_pojemnik]};
	#unvariable dopelniacz
}
{5}

#ALIAS {/u}
{
	#if {$ukrywanie == 1}
	{
		ujawnij sie
	};
	#else
	{
		ukryj sie
	}
}
{5}

#ALIAS {/ustawpogon}
{
	#nop possible bug in if conditions so this looks like this for now;
	#if {&{postacie[$nazwa_postaci][ustawienia][pogon]}}
	{
		#if {${postacie[$nazwa_postaci][ustawienia][pogon]} == 1}
		{
			#variable postacie[$nazwa_postaci][ustawienia][pogon] 0;
			save
		};
		#else
		{
			#variable postacie[$nazwa_postaci][ustawienia][pogon] 1;
			save
		}
	};
	#else
	{
		#variable postacie[$nazwa_postaci][ustawienia][pogon] 1;
		save
	}
}
{5}

#ALIAS {kk}
{
	kondycja wszystkich
}
{5}

#FUNCTION {rodzaj_ataku}
{
	#switch {"%0"}
	{
		#case {"{?i}{szybkim pchnieciem|mizernym cieciem|swietnym pchnieciem|niesamowitym cieciem|genialnym cieciem|slabiutkim pchnieciem|mizernym pchnieciem|silnym cieciem|dosc silnym cieciem|dosyc szybkim cieciem|idealnie wymierzonym cieciem|niezwykle mocnym cieciem|celnym pchnieciem|zaskakujacym cieciem|bardzo silnym pchnieciem|nieoczekiwanie dobrym cieciem|fantastycznym cieciem|szybkim cieciem|mocnym cieciem|bardzo mocnym pchnieciem|niezlym pchnieciem|blyskawicznym cieciem|bardzo mocnym cieciem|dosc zaskakujacym cieciem|dosyc udanym pchnieciem|swietnym pchnieciem|zaskakujacym pchnieciem|prymitywnym cieciem|idealnym cieciem|niezwykle mocnym pchnieciem|bardzo szybkim pchnieciem|niekonwencjonalnym pchnieciem|silnym pchnieciem|niesamowitym pchnieciem|celnym cieciem|mocnym pchnieciem|dosc zaskakujacym pchnieciem|poteznym pchnieciem|niezlym cieciem|wspanialym pchnieciem|dosyc celnym cieciem|powolnym cieciem|dosyc udanym cieciem|starannie wymierzonym cieciem|swietnym cieciem|wspanialym cieciem|bardzo celnym cieciem|bardzo prymitywnym cieciem|bardzo powolnym pchnieciem|kompletnie niecelnym cieciem|kompletnie niecelnym pchnieciem|niekonwencjonalnym cieciem|nieoczekiwanie dobrym pchnieciem|bardzo powolnym cieciem|calkiem niezlym pchnieciem|niecelnym pchnieciem|dosyc silnym pchnieciem|mierzonym cieciem|starannie wymierzonym pchnieciem|slabiusienkim cieciem|bardzo celnym pchnieciem|idealnie wymierzonym pchnieciem|genialnym pchnieciem|bardzo mizernym cieciem|bardzo szybkim cieciem|fantastycznym pchnieciem|bardzo niecelnym pchnieciem|slabiutkim cieciem|nieskoordynowanym cieciem}"}
		{
			#return <afa>%0<099>
		};
		#default
		{
			#return %0<faa>[BRAK ATAKU]<099>
		}
	}
}

#FUNCTION {rodzaj_obrazen}
{
	#switch {"%0"}
	{
		#case {"{?i}{nie udaje ci sie trafic|probujesz trafic}"}
		{
			#return <bfa>%0<170>[0/5]<099>
		};
		#case {"{?i}{probuje cie trafic|nie udaje sie trafic ciebie}"}
		{
			#return <bfa>%0<170>[0/5]<099>
		};
		#case {"{?i}{ledwo muskasz|ledwo nacinasz|ledwie nacinasz|ledwie tniesz|ledwie nakluwasz|zadajesz niewielkie obrazenia|zadajesz nieznaczne obrazenia}"}
		{
			#return <cfa>%0<170>[1/5]<099>
		};
		#case {"{?i}{ledwo muska cie}"}
		{
			#return <cfa>%0<170>[1/5]<099>
		};
		#case {"{?i}{lekko ranisz|zadajesz lekkie obrazenia|lekko nacinasz|lekko obijasz|lekko klujesz|lekko tniesz}"}
		{
			#return <dfa>%0<170>[2/5]<099>
		};
		#case {"{?i}{lekko rani cie}"}
		{
			#return <dfa>%0<170>[2/5]<099>
		};
		#case {"{?i}{ranisz|klujesz|tniesz|obijasz|siniaczysz|nacinasz|nakluwasz|klujesz|nacinasz|rani cie}"}
		{
			#return <fca>%0<170>[3/5]<099>
		};
		#case {"{?i}{rani cie}"}
		{
			#return <fca>%0<170>[3/5]<099>
		};
		#case {"{?i}{bardzo mocno ranisz|bardzo mocno klujesz|bardzo mocno siniaczysz|bardzo mocno tniesz|bardzo mocno obijasz|bardzo gleboko nacinasz|mocno kaleczysz|bardzo ciezko ranisz|powaznie tniesz|bolesnie klujesz|powaznie ranisz|zadajesz bardzo ciezkie rany|zadajesz spore obrazenia|mocno nacinasz|zadajesz powazne obrazenia|gleboko ranisz|bardzo gleboko klujesz|zadajesz ogromne obrazenia|z ogromna sila przebijasz}"}
		{
			#return <fba>%0<170>[4/5]<099>
		};
		#case {"{?i}{powaznie rani cie}"}
		{
			#return <fba>%0<170>[4/5]<099>
		};
		#case {"{?i}{masakrujesz|smiertelnie nacinasz|zadajesz smiertelne obrazenia|patroszysz}"}
		{
			#return <faa>%0<170>[5/5]<099>
		};
		#default
		{
			#return %0<faa>[BRAK OBRAZEN]<099>
		}
	}
}

#SUBSTITUTE       {^%1 jest %2.}  {%1 jest @kondycja{%2}.}  {5}
#SUBSTITUTE       {^%1 swoje{go|j} %3 {nie udaje ci sie trafic|probujesz trafic|ledwo muskasz|ledwo nacinasz|ledwie nacinasz|ledwie tniesz|ledwie nakluwasz|zadajesz niewielkie obrazenia|zadajesz nieznaczne obrazenia|lekko ranisz|zadajesz lekkie obrazenia|lekko nacinasz|lekko obijasz|lekko klujesz|lekko tniesz|ranisz|klujesz|tniesz|obijasz|siniaczysz|nacinasz|nakluwasz|klujesz|nacinasz|bardzo mocno ranisz|bardzo mocno klujesz|bardzo mocno siniaczysz|bardzo mocno tniesz|bardzo mocno obijasz|bardzo gleboko nacinasz|mocno kaleczysz|bardzo ciezko ranisz|powaznie tniesz|bolesnie klujesz|powaznie ranisz|zadajesz bardzo ciezkie rany|zadajesz spore obrazenia|mocno nacinasz|zadajesz powazne obrazenia|masakrujesz|smiertelnie nacinasz|zadajesz smiertelne obrazenia|patroszysz|gleboko ranisz|bardzo gleboko klujesz|zadajesz ogromne obrazenia|z ogromna sila przebijasz} %5, trafiajac {go|ja} w %7.$}                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                         {@rodzaj_ataku{%1} swoje%2 %3 @rodzaj_obrazen{%4} %5, trafiajac %6 w %7.}                                                {5}
#SUBSTITUTE       {^%1 wpada w panike!$}  {<afa>%0<099>}  {5}
#SUBSTITUTE       {^%1 {ledwo muska cie|rani cie|lekko rani cie|powaznie rani cie} %3, trafiajac cie w %4.$}                                                                      {%1 @rodzaj_obrazen{%2} %3, trafiajac cie w %4.}                       {5}
#SUBSTITUTE       {^%1 {nie udaje sie trafic ciebie} %3.$}                    {%1 @rodzaj_obrazen{%2} %3.}   {5}
#SUBSTITUTE       {^%1 {probuje cie trafic} %3, lecz tobie udaje sie uniknac tego ciosu.$}                                                    {%1 @rodzaj_obrazen{%2} %3, lecz tobie udaje sie uniknac tego ciosu.}                                            {5}
#SUBSTITUTE       {^Niezauwazony podkradasz sie do %1 i naglym ruchem wbijasz swoj %2 w plecy, %3.$}                                                              {<afa>Niezauwazony podkradasz sie do %1 i naglym ruchem wbijasz swoj %2 w plecy, %3.<088>}                                                                 {5}
#SUBSTITUTE       {^Sciskasz mocniej %1 i zaczynasz sie skradac w kierunku %2.$}                                          {<afa>%0<088>}  {5}
#SUBSTITUTE       {^\b{?i}{nie udaje ci sie trafic|probujesz trafic|ledwo muskasz|ledwo nacinasz|ledwie nacinasz|ledwie tniesz|ledwie nakluwasz|zadajesz niewielkie obrazenia|zadajesz nieznaczne obrazenia|lekko ranisz|zadajesz lekkie obrazenia|lekko nacinasz|lekko obijasz|lekko klujesz|lekko tniesz|ranisz|klujesz|tniesz|obijasz|siniaczysz|nacinasz|nakluwasz|klujesz|nacinasz|bardzo mocno ranisz|bardzo mocno klujesz|bardzo mocno siniaczysz|bardzo mocno tniesz|bardzo mocno obijasz|bardzo gleboko nacinasz|mocno kaleczysz|bardzo ciezko ranisz|powaznie tniesz|bolesnie klujesz|powaznie ranisz|zadajesz bardzo ciezkie rany|zadajesz spore obrazenia|mocno nacinasz|zadajesz powazne obrazenia|masakrujesz|smiertelnie nacinasz|zadajesz smiertelne obrazenia|patroszysz|gleboko ranisz|bardzo gleboko klujesz|zadajesz ogromne obrazenia|z ogromna sila przebijasz}\b}                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  {@rodzaj_obrazen{%1}}  {5}
#SUBSTITUTE       {^{Jestes|Zdaje sie byc} %2.}         {%1 @kondycja{%2}.}  {5}
#VARIABLE         {dzis_zabitych}  {0}
#VARIABLE         {enemy_run}  {0}
#VARIABLE         {run_direction}  {spojrz}
#VARIABLE         {ukrywanie}  {0}
#VARIABLE         {wycinka_komenda}  {spojrz}
#VARIABLE         {wycinka_skor}  {0}

#CLASS {walka} CLOSE
