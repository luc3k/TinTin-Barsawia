#CLASS {zielarstwo} OPEN

#ACTION {^Jestes teraz zajety praca. Musisz wpisac 'przestan' by moc zrobic, to co chcesz.$}
{
	#map undo
}
{5}

#ACTION {^Zaczynasz szukac {ziol|macierzanki}.$}
{
	#line gag;
	#echo {<afa>%0<099>};
	#variable aktywne_szukanie 1
}
{5}

#ACTION {^Znajdujesz {?!sie}{.*}{\bkwiat\b|\blisc\b|\bkorzen\b|\blodyge\b|\bowoc\b}{.*}$}
{
	#variable aktywne_szukanie 0;
	otworz sloik;
	wloz ziola do sloika;
	zamknij sloik;
	#math {liczba_zeb_ziol} {${liczba_zeb_ziol}+1};
	#math {dzis_zeb_ziol} {${dzis_zeb_ziol}+1};
	save;
	#line GAG;
	#echo {<afa>%0<099>}
}
{5}

#ACTION {^{Szukasz wszedzie, ale nie znajdujesz zadnych ziol.|Nie znajdujesz zadnych ziol.|Nie znajdujesz zadnych ziol. Moze poszloby ci lepiej, gdybys zaczekal do wiosny.|Nie udalo ci sie znalezc niczego interesujacego.}$}
{
	#echo {<fab>%0<099>};
	#variable {aktywne_szukanie} {0};
	#line gag
}
{5}

#ALIAS {/sellz}
{
	otworz worek;
	wez sloik z worka;
	otworz sloik;
	wez ziola ze sloika;
	zamknij sloik;
	wloz sloik do worka;
	zamknij worek;
	sprzedaj ziola
}
{5}

#ALIAS {/szukaj}
{
	#if {$szukanie_ziol == 0}
	{
		otworz worek;
		wez sloik z worka;
		zamknij worek;
		#variable {szukanie_ziol} {1};
		#cr
	};
	#elseif {$szukanie_ziol == 1}
	{
		otworz worek;
		wloz sloik do worka;
		zamknij worek;
		#variable {szukanie_ziol} {0};
		#cr
	}
}
{5}

#SUBSTITUTE       {{?i}bial{y|e|ych} drobn{y|e|ych} \bkwiat{y|ow|}\b}                               {%0 <afa>(rumianek)<099>}  {5}
#SUBSTITUTE       {{?i}bial{y|e|ych} okazal{y|e|ych} \bkorzen{ie|i|}\b}                                 {%0 <afa>(biedrzawiec)<099>}   {5}
#SUBSTITUTE       {{?i}czerwonaw{y|e|ych} ostr{y|e|ych} \blisc{ie|i|}\b}                                  {%0 <afa>(szczaw)<099>}  {5}
#SUBSTITUTE       {{?i}drobn{y|e|ych} rozow{y|e|ych} \bkwiat{y|ow|}\b}                                {%0 <afa>(macierzanka)<099>}   {5}
#SUBSTITUTE       {{?i}duz{y|e|ych} czerwon{y|e|ych} \bkwiat{y|ow|}\b}                                {%0 <afa>(mak)<099>}  {5}
#SUBSTITUTE       {{?i}malutk{i|ie|ich} bial{y|e|ych} \bkwiat{y|ow|}\b}                                 {%0 <afa>(stokrotka)<099>}  {5}
#SUBSTITUTE       {{?i}podluzn{y|e|ych} cienk{i|ie|ich} \blisc{ie|i|}\b}                                  {%0 <afa>(trawa)<099>}  {5}
#VARIABLE         {aktywne_szukanie}  {0}
#VARIABLE         {dzis_zeb_ziol}  {0}
#VARIABLE         {szukanie_ziol}  {0}

#CLASS {zielarstwo} CLOSE
